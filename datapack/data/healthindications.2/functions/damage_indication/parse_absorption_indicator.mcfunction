# parse absorption indicator

#set symbol
execute if score #healthindications.absorption_difference sourcecraft.temp matches 1.. run data modify storage healthindications damage_indication.symbol set value '"+"'
execute if score #healthindications.absorption_difference sourcecraft.temp matches ..0 run data modify storage healthindications damage_indication.symbol set value ''

#parse
execute if score #healthindications.option.damage_indication.no_symbol sourcecraft.data matches 1 at @e[type=minecraft:marker,tag=sourcecraft.nbt_sign] run data modify block ~ ~ ~ Text1 set value '[{"nbt":"damage_indication.symbol","storage":"healthindications","interpret":true,"color":"yellow"},{"score":{"name":"#healthindications.absorption_difference","objective":"sourcecraft.temp"}}]'
execute unless score #healthindications.option.damage_indication.no_symbol sourcecraft.data matches 1 if score #healthindications.option.damage_indication.custom_texture sourcecraft.data matches 1 at @e[type=minecraft:marker,tag=sourcecraft.nbt_sign] run data modify block ~ ~ ~ Text1 set value '[{"nbt":"damage_indication.symbol","storage":"healthindications","interpret":true,"color":"yellow"},{"score":{"name":"#healthindications.absorption_difference","objective":"sourcecraft.temp"}},{"font":"healthindications:heart","translate":"icon.healthindications.damage_indication.absorption_heart","color":"white"}]'
execute unless score #healthindications.option.damage_indication.no_symbol sourcecraft.data matches 1 unless score #healthindications.option.damage_indication.custom_texture sourcecraft.data matches 1 at @e[type=minecraft:marker,tag=sourcecraft.nbt_sign] run data modify block ~ ~ ~ Text1 set value '[{"nbt":"damage_indication.symbol","storage":"healthindications","interpret":true,"color":"yellow"},{"score":{"name":"#healthindications.absorption_difference","objective":"sourcecraft.temp"}},{"nbt":"icon.damage_indication.absorption_heart","storage":"healthindications","interpret":true}]'